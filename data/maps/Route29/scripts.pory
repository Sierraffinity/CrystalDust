mapscripts Route29_MapScripts{
	MAP_SCRIPT_ON_TRANSITION{
		gettime
		if(!flag(FLAG_BADGE01_GET) || var(VAR_0x8003) != DAY_TUESDAY)
		{
			setflag(FLAG_TEMP_1)
		}
	}
}

script Route29_EventScript_Tuscany {
	lock
	faceplayer
	if (!flag(FLAG_RECEIVED_SILK_SCARF_FROM_TUSCANY))
	{
		gettime
		if (var(VAR_0x8003) == DAY_TUESDAY)
		{
			if (!flag(FLAG_MET_TUSCANY_OF_TUESDAY))
			{
				msgbox("TUSCANY: I do believe that this\nis the first time we've met?\pPlease allow me to introduce\nmyself.\pI am TUSCANY of TUESDAY.\pBy way of introduction, please\naccept this gift, a SILK SCARF.")
				setflag(FLAG_MET_TUSCANY_OF_TUESDAY)
			}
			else
			{
				msgbox("TUSCANY: By way of introduction,\nplease accept this gift, a\lSILK SCARF.")
			}
			giveitem(ITEM_SILK_SCARF)
			if (!var(VAR_RESULT))
			{
				goto(Common_EventScript_ShowBagIsFull)
			}
			setflag(FLAG_RECEIVED_SILK_SCARF_FROM_TUSCANY)
			msgbox("TUSCANY: Wouldn't you agree that\nit is most adorable?\pIt strengthens NORMAL-type moves.\nI am certain it will be of use.")
		}
		else
		{
			msgbox("TUSCANY: Today is not TUESDAY.\nThat is unfortunate…")
		}
	}
	else
	{
		msgbox("TUSCANY: Have you met MONICA,\nmy older sister?\lOr my younger brother, WESLEY?\pI am the second of seven children.")
	}
	release
}

script Route29_EventScript_Youngster{
	msgbox("Yo.\nHow are your {POKEMON}?\pIf they're weak and not ready for\nbattle, keep out of the grass.", MSGBOX_NPC)
	end
}

script Route29_EventScript_LedgeGirl{
	msgbox("See those ledges?\nIt's scary to jump off them.\pBut you can go to NEW BARK without\nwalking through the grass.", MSGBOX_NPC)
	end
}

script Route29_EventScript_FatMan{
	msgbox("I wanted to take a break, so I\nsaved to record my progress.", MSGBOX_NPC)
	end
}

script Route29_EventScript_WaitingMan{
	lock
	faceplayer
	gettime
	if(var(VAR_0x8002) == TIME_NIGHT)
	{
		bufferstring(0, " in the morning.")
	}
	else
	{
		bufferstring(0, " at night.")
	}
	msgbox("I'm waiting for {POKEMON} that appear\nonly{STR_VAR_1}")
	release
	end
}

script Route29_EventScript_DirectionSign{
	msgbox("ROUTE 29\nCHERRYGROVE CITY - NEW BARK TOWN", MSGBOX_SIGN)
	end
}

script Route29_EventScript_Dude{
	lock
	faceplayer
	if(flag(FLAG_LEARNED_TO_CATCH_POKEMON) || !flag(FLAG_GAVE_MYSTERY_EGG_TO_ELM))
	{
		msgbox("{POKEMON} hide in the grass.\nWho knows when they'll pop out…")
		release
		end
	}
	msgbox("Huh? You want me to show you how\nto catch {POKEMON}?", MSGBOX_YESNO)
	if(!var(VAR_RESULT))
	{
		msgbox(Route29_Text_Dude_CatchingTutRejected)
		release
		end
	}
	closemessage
	special(StartDudeTutorialBattle)
	waitstate
	lock //hack
	msgbox(Route29_Text_Dude_CatchingOutro)
	setflag(FLAG_LEARNED_TO_CATCH_POKEMON)
	release
	end
}text(local) Route29_Text_Dude_CatchingTutRejected{
	"Oh.\nFine, then.\pAnyway, if you want to catch\n{POKEMON}, you have to walk a lot."
}text(local) Route29_Text_Dude_CatchingOutro{
	"That's how you do it.\pIf you weaken them first, {POKEMON}\nare easier to catch."
}

script Route29_EventScript_CatchingTutorialTriggers{
	lockall
	getplayerxy(VAR_RESULT, VAR_TEMP_1)
	turnobject(1, DIR_NORTH)
	playse(SE_PIN)
	applymovement(1, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(1, Common_Movement_Delay48)
	waitmovement(0)
	switch(var(VAR_TEMP_1))
	{
		case 10:
			applymovement(1, Route29_Movement_DudeToPlayerTop)
		case 11:
			applymovement(1, Route29_Movement_DudeToPlayerMiddleTop)
		case 12:
			applymovement(1, Route29_Movement_DudeToPlayerMiddleBottom)
		case 13:
			applymovement(1, Route29_Movement_DudeToPlayerBottom)
	}
	waitmovement(1)
	textcolor(MSG_COLOR_BLUE)
	setflag(FLAG_DUDE_TALKED_TO_YOU)
	setvar(VAR_ROUTE29_STATE, 2)
	msgbox("I've seen you a couple times.\nHow many {POKEMON} have you caught?\pWould you like me to show you how\nto catch {POKEMON}?", MSGBOX_YESNO)
	if(!var(VAR_RESULT))
	{
		msgbox(Route29_Text_Dude_CatchingTutRejected)
		closemessage
		switch(var(VAR_TEMP_1))
		{
			case 10:
				applymovement(1, Route29_Movement_DudeFromTop)
			case 11:
				applymovement(1, Route29_Movement_DudeFromMiddleTop)
			case 12:
				applymovement(1, Route29_Movement_DudeFromMiddleBottom)
			case 13:
				applymovement(1, Route29_Movement_DudeFromBottom)
		}
		waitmovement(1)
		releaseall
		end
	}
	closemessage
	switch(var(VAR_TEMP_1))
	{
		case 10:
			applymovement(OBJ_EVENT_ID_PLAYER, Route29_Movement_PlayerFromTop)
			applymovement(1, Route29_Movement_DudeFromTop)
			waitmovement(0)
		case 11:
			applymovement(OBJ_EVENT_ID_PLAYER, Route29_Movement_PlayerFromMiddleTop)
			applymovement(1, Route29_Movement_DudeFromMiddleTop)
			waitmovement(0)
		case 12:
			applymovement(OBJ_EVENT_ID_PLAYER, Route29_Movement_PlayerFromMiddleBottom)
			applymovement(1, Route29_Movement_DudeFromMiddleBottom)
			waitmovement(0)
		case 13:
			applymovement(OBJ_EVENT_ID_PLAYER, Route29_Movement_PlayerFromBottom)
			applymovement(1, Route29_Movement_DudeFromBottom)
			waitmovement(0)
	}
	special(StartDudeTutorialBattle)
	waitstate
	turnobject(1, DIR_NORTH)
	msgbox(Route29_Text_Dude_CatchingOutro)
	setflag(FLAG_LEARNED_TO_CATCH_POKEMON)
	releaseall
	end
}movement(local) Route29_Movement_DudeToPlayerTop{
	walk_up * 5
	walk_right * 2
}movement(local) Route29_Movement_DudeToPlayerMiddleTop{
	walk_up * 4
	walk_right * 2
}movement(local) Route29_Movement_DudeToPlayerMiddleBottom{
	walk_up * 3
	walk_right * 2
}movement(local) Route29_Movement_DudeToPlayerBottom{
	walk_up * 2
	walk_right * 2
}movement(local) Route29_Movement_DudeFromTop{
	walk_left * 2
	walk_down * 5
}movement(local) Route29_Movement_DudeFromMiddleTop{
	walk_left * 2
	walk_down * 4
}movement(local) Route29_Movement_DudeFromMiddleBottom{
	walk_left * 2
	walk_down * 3
}movement(local) Route29_Movement_DudeFromBottom{
	walk_left * 2
	walk_down * 2
}movement(local) Route29_Movement_PlayerFromTop{
	walk_left * 3
	walk_down * 4
}movement(local) Route29_Movement_PlayerFromMiddleTop{
	walk_left * 3
	walk_down * 3
}movement(local) Route29_Movement_PlayerFromMiddleBottom{
	walk_left * 3
	walk_down * 2
}movement(local) Route29_Movement_PlayerFromBottom{
	walk_left * 3
	walk_down
}
