mapscripts Route33_MapScripts {}

script Route33_EventScript_Girl {
    msgbox("Pant, pant…\nI finally got through that cave.\pIt was so much bigger than I'd\nexpected.\pI got too tired to explore the whole\nthing, so I came outside.", MSGBOX_NPC)
}

script Route33_EventScript_Anthony {
    trainerbattle_single(TRAINER_ANTHONY_1, "I came through the tunnel, but I\nstill have plenty of energy left!", "Whoa!\nYou've got more zip than me!")
    // TODO: Check phone strings for textbox fit
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == 1)
    {
        trainerbattle_rematch(TRAINER_ANTHONY_1, "All righty, then!\nMy {POKEMON} and I are raring to go!", "Whoa!\nYou've got more zip than me!")
        clearflag(FLAG_ANTHONY_OFFERED_REMATCH)
    }		  
    if (!flag(FLAG_PHONE_CARD_HIKER_ANTHONY))
    {
        if (!flag(FLAG_HIKER_ANTHONY_OFFERED_NUMBER))
        {
            msgbox("By the way, I'm a HIKER, so\nI often go to mountains and caves.\pI occasionally see rare {POKEMON}.\lI could call you\lthe next time I see one.\lFeel like swapping phone numbers?", MSGBOX_YESNO)
            setflag(FLAG_HIKER_ANTHONY_OFFERED_NUMBER)
        }
        else
        {
            msgbox("I could call you when I see some\nrare {POKEMON}.\pFeel like swapping phone numbers?", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == 1)
        {
            registerphonecontact(FLAG_PHONE_CARD_HIKER_ANTHONY)
            msgbox("Be patient. I'll find some rare\n{POKEMON} for you!", MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("You're cold. I may as well head to\nthe mountains…", MSGBOX_AUTOCLOSE)
        }
        end
    }
    msgbox("We HIKERS are at our best in\nthe mountains.")
    release
}

script Route33_EventScript_DirectionsSign {
    msgbox("ROUTE 33", MSGBOX_SIGN)
}
