mapscripts VioletCity_House2_MapScripts { }

script VioletCity_House2_EventScript_Man {
    msgbox("A POKéMON you get in a trade\n"
           "grows quickly.\p"
           "But if you don't have the right\n"
           "GYM BADGE, they may disobey you.", MSGBOX_NPC)
}

script VioletCity_House2_EventScript_Kyle {
    lock
    faceplayer
    setvar(VAR_0x8008, INGAME_TRADE_ONIX)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)

    if (!flag(FLAG_VIOLET_NPC_TRADE_COMPLETED))
    {
        msgbox("I collect POKéMON.\n"
               "Do you have {STR_VAR_1}?\p"
               "Want to trade it for my {STR_VAR_2}?", MSGBOX_YESNO)
        
        if (var(VAR_RESULT) != NO)
        {
            special(ChoosePartyMon)
            waitstate
            lock
            faceplayer
            copyvar(VAR_0x800A, VAR_0x8004)
            
            if (var(VAR_0x8004) != 0xFF)
            {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                copyvar(VAR_0x800B, VAR_RESULT)

                if (var(VAR_RESULT) == VAR_0x8009)
                {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)
                    waitstate
                    lock
                    faceplayer
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Yay! I got myself {STR_VAR_1}!\n"
                           "Thanks!")
                    setflag(FLAG_VIOLET_NPC_TRADE_COMPLETED)
                }
                else
                {
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Huh? That's not {STR_VAR_1}.\n"
                           "What a letdown…")
                }
            }
            else
            {
                msgbox(VioletCity_House2_Text_Kyle_NoTrade)
            }
        }
        else
        {
            msgbox(VioletCity_House2_Text_Kyle_NoTrade)
        }
    }
    else
    {
        bufferspeciesname(0, VAR_0x8009)
        msgbox("Hi, how's my old {STR_VAR_2} doing?")
    }

    release
}

text VioletCity_House2_Text_Kyle_NoTrade {
    "You don't want to trade?\n"
    "Aww…"
}
