mapscripts Route31_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_ROUTE31_STATE, 0 {
            lockall
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            pokegearcall(PhoneScript_Mom, 0)
            waitstate
            clearflag(FLAG_HIDE_MAP_NAME_POPUP)
            setvar(VAR_ROUTE31_STATE, 1)
            releaseall
        }
    ]
}

script Route31_EventScript_Explorer {
    msgbox("DARK CAVE…\pIf {POKEMON} could light it up, I'd\nexplore it.", MSGBOX_NPC)
}

script Route31_EventScript_SleepyMan {
    lock
    faceplayer
    if (flag(FLAG_MOVE_TUTOR_TAUGHT_NIGHTMARE)) {
        msgbox(Route31_Text_SleepyMan_TMDescription)
    }
    elif(flag(FLAG_GAVE_KENYA_TO_RANDYS_FRIEND)) {
        msgbox("…Zzzz…\nHuh?\pOh, it's you again! Thanks for\nbringing that MAIL to me.\pMy friend's a good guy, and you're\nswell too!\pI'd like to do something good in\nreturn for your help!\pI know!\pHow about I teach your {POKEMON}\nNIGHTMARE for helping me?", MSGBOX_YESNO)
        if (var (VAR_RESULT)) {
            call(Route31_EventScript_SleepyMan_TryGiveTM)
        } else {
            msgbox(Route31_Text_SleepyMan_Declined)
        }
    }
    elif(flag(FLAG_GOT_KENYA_FROM_RANDY)) {
        msgbox("…Zzzz…\nHuh?\pWhat's that?\nYou have MAIL for me?")
        special(ChoosePartyMon)
        waitstate
        lock
        faceplayer
        if (var (VAR_0x8004) != 0xFF) {
            setvar(VAR_0x8005, INGAME_TRADE_GIFT_SPEAROW)
            special(CheckForGiftMonAndTakeMail)
            switch (var (VAR_RESULT)) {
                case GIFTMON_NO_MAIL:
                    msgbox("Why is this {POKEMON} so special?\nIt doesn't have any MAIL.")
                case GIFTMON_LAST_MON:
                    msgbox("If I take that {POKEMON} from you,\nwhat are you going to use in battle?")
                case GIFTMON_WRONG_MAIL:
                    msgbox("This MAIL isn't for me.")
                default:
                    if (var (VAR_RESULT) == GIFTMON_WRONG_MON) {
                        msgbox("…Huh? Who's that {POKEMON}?\nIt's not one I know.\pYou can keep it, and I'll just\ntake the MAIL.")
                        textcolor(MSG_COLOR_SYS)
                        playfanfare(MUS_OBTAIN_ITEM)
                        message("{PLAYER} handed over the MAIL.")
                    }
                    else {// GIFTMON_MATCH
                        textcolor(MSG_COLOR_SYS)
                        playfanfare(MUS_OBTAIN_ITEM)
                        msgbox("{PLAYER} handed over the {POKEMON}\n"
                        "holding the MAIL.")
                    }
                    setflag(FLAG_GAVE_KENYA_TO_RANDYS_FRIEND)
                    waitmessage
                    waitfanfare
                    textcolor(MSG_COLOR_PREV)
                    msgbox("Let's see…\p“There's a dark place near you.\nLet's go have an adventure there!”\pDoes he mean DARK CAVE?\nThat sounds like fun.\pYou know, I heard that cave leads\nto another road.\pAnyway, thanks for bringing this\nto me.\pMy friend's a good guy, and you're\nswell too!\pI'd like to do something good in\nreturn for your help!\pI know!\pHow about I teach your {POKEMON}\nNIGHTMARE for helping me?", MSGBOX_YESNO)
                    if (var (VAR_RESULT)) {
                        call(Route31_EventScript_SleepyMan_TryGiveTM)
                    } else {
                        msgbox(Route31_Text_SleepyMan_Declined)
                    }
            }
        } else {
            msgbox(Route31_Text_SleepyMan_Declined)
        }
    }
    else {
        msgbox("…Hnuurg… Huh?\pI walked too far today looking\nfor {POKEMON}.\pMy feet hurt and I'm sleepy…\pIf I were a wild {POKEMON}, I'd be\neasy to catch…\p…Zzzz…")
    }
    release
}

text Route31_Text_SleepyMan_Declined {
    "What?\nYou don't want anything?"
}

text Route31_Text_SleepyMan_TMDescription {
    "That's NIGHTMARE.\pIt's a wicked move that steadily cuts\nthe HP of a sleeping enemy.\pOoooh…\nThat's scary…\lI don't want to have bad dreams."
}

script Route31_EventScript_SleepyMan_TryGiveTM {
    call(MoveTutor_EventScript_CanOnlyBeLearnedOnce)
    if (!var(VAR_RESULT)) {
        goto(MoveTutor_EventScript_NightmareDeclined)
    }
    msgbox("Which {POKEMON} should learn\nNIGHTMARE?", MSGBOX_DEFAULT)
    setvar(VAR_0x8005, TUTOR_MOVE_NIGHTMARE)
    call(MoveTutor_EventScript_OpenPartyMenu)
    if (var (VAR_RESULT) == 0) {
        goto(MoveTutor_EventScript_NightmareDeclined)
    }
    setflag(FLAG_MOVE_TUTOR_TAUGHT_NIGHTMARE)
    goto(Route31_EventScript_SleepyMan)
    end
}

script MoveTutor_EventScript_NightmareDeclined {
    msgbox(Route31_Text_SleepyMan_Declined)
    release
    end
}

script Route31_EventScript_Youngster {
    msgbox("I found a good {POKEMON} in\nDARK CAVE.\pI'm going to raise it to take on\nFALKNER, the leader of VIOLET\lCITY's GYM.", MSGBOX_NPC)
}

script Route31_EventScript_Trainer_Wade {
    trainerbattle_single(TRAINER_WADE_1, "I caught a bunch of {POKEMON}.\nLet me battle with you!", "Awwwww…")
    // TODO: Check phone strings for textbox fit

    if(flag(FLAG_CALL_WADE_GIFT))
    {
        msgbox("Good to see you!\nSee? This is what I found.\lIt's for you.", MSGBOX_AUTOCLOSE)
        random(42)
        addvar(VAR_RESULT,133)
        giveitem(VAR_RESULT, 1)
        if(var(VAR_0x8007) == FALSE)
        {
            msgbox("Oops, your PACK's full. Tood bad.\nYou should come back for it later.", MSGBOX_AUTOCLOSE)
        }
        else
        {
            clearflag(FLAG_CALL_WADE_GIFT)
        }
        end
    }
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == 1)
    {
        trainerbattle_rematch(TRAINER_WADE_1, "I was waiting for you! Let's battle!", "Awwwww…")
        clearflag(FLAG_WADE_OFFERED_REMATCH)
    }

    if (!flag(FLAG_PHONE_CARD_BUG_CATCHER_WADE))
    {
        if (!flag(FLAG_BUG_CATCHER_WADE_OFFERED_NUMBER))
        {
            msgbox("You're awesome! I like you! I look\nfor {POKEMON} here every day.\pWhen I'm in the grass,\nI find all kinds of BERRIES.\lIf you'd like, I'll share some.\pCould I get your phone number?", MSGBOX_YESNO)
            setflag(FLAG_BUG_CATCHER_WADE_OFFERED_NUMBER)
        }
        else
        {
            msgbox("When I'm in the grass, I find all\nkinds of BERRIES.\lIf you'd like, I'll share some.\pCould I get your phone number?", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == 1)
        {
            registerphonecontact(FLAG_PHONE_CARD_BUG_CATCHER_WADE)
            msgbox("You'll hear from me as soon as I\nfind anything that you might like.", MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("Oh well. Don't be shy if you want\nto get my number.", MSGBOX_AUTOCLOSE)
        }
        end
    }
    msgbox("You can catch {POKEMON} even if you\nhave six with you.\pIf you catch one, it'll go to your\nBOX automatically.", MSGBOX_DEFAULT)
    release
}

script Route31_EventScript_Directions {
    msgbox("ROUTE 31\nVIOLET CITY - CHERRYGROVE CITY", MSGBOX_SIGN)
}

script Route31_EventScript_DarkCave {
    msgbox("DARK CAVE", MSGBOX_SIGN)
}
