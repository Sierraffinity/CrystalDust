const ROADBLOCK_MAN = 1
const SLOWPOKETAIL_MAN = 12

mapscripts Route32_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        gettime
        if (var (VAR_0x8003) == DAY_FRIDAY) {
            clearflag(FLAG_HIDE_ROUTE_32_FRIEDA)
        } else {
            setflag(FLAG_HIDE_ROUTE_32_FRIEDA)
        }
    }
}

script Route32_EventScript_RoadblockManTrigger_1 {
    lockall
    setvar(VAR_TEMP_1, 0)
    goto(Route32_EventScript_RoadblockManTrigger_All)
}

script Route32_EventScript_RoadblockManTrigger_2 {
    lockall
    setvar(VAR_TEMP_1, 1)
    goto(Route32_EventScript_RoadblockManTrigger_All)
}

script Route32_EventScript_RoadblockManTrigger_All {
    playse(SE_PIN)
    applymovement(ROADBLOCK_MAN, Route32_Movement_RoadblockMan_NoticePlayer)
    waitmovement(0)
    applymovement(ROADBLOCK_MAN, Common_Movement_Delay48)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestRight)
    waitmovement(0)
    textcolor(MSG_COLOR_BLUE)
    msgbox("Wait up!\nWhat's the hurry?")
    closemessage

    if (var (VAR_TEMP_1) == 0) {
        applymovement(OBJ_EVENT_ID_PLAYER, Route32_Movement_RoadblockMan_PlayerFromLeft)
        applymovement(ROADBLOCK_MAN, Route32_Movement_RoadblockMan_ManFromLeft)
        waitmovement(0)
    } else {
        applymovement(OBJ_EVENT_ID_PLAYER, Route32_Movement_RoadblockMan_PlayerFromRight)
        applymovement(ROADBLOCK_MAN, Route32_Movement_RoadblockMan_ManFromRight)
        waitmovement(0)
    }

    call(Route32_EventScript_RoadblockMan_Common)
    closemessage

    if (var (VAR_TEMP_1) == 0) {
        applymovement(ROADBLOCK_MAN, Route32_Movement_RoadblockMan_ManResetFromLeft)
        waitmovement(0)
    } else {
        applymovement(ROADBLOCK_MAN, Route32_Movement_RoadblockMan_ManResetFromRight)
        waitmovement(0)
    }
    releaseall
}

script Route32_EventScript_RoadblockMan {
    lock
    faceplayer
    call(Route32_EventScript_RoadblockMan_Common)
    release
}

script Route32_EventScript_RoadblockMan_Common {
    if (flag(FLAG_GOT_MIRACLE_SEED_ROUTE_32)) {
        call(Route32_EventScript_RoadblockMan_AfterItem)
    }
    elif(!flag(FLAG_BADGE01_GET)) {
        msgbox("Have you gone to the {POKEMON}\nGYM in VIOLET yet?\pYou can test your {POKEMON} and\nyourself there.\pIt's a rite of passage for all\nTRAINERS!")
    }
    elif(flag(FLAG_GOT_ELMS_EGG)) {
        msgbox("Looks like you have some good\n{POKEMON} there.\pIt must be from the training you\ngave them around VIOLET CITY.\pThe training at the GYM must have\nbeen especially helpful.\pAs a souvenir of VIOLET CITY,\ntake this.\pIt increases the power of GRASS-\ntype moves.")
        giveitem(ITEM_MIRACLE_SEED)
        if (!var(VAR_RESULT)) {
            goto(Common_EventScript_ShowBagIsFull)
        } else {
            setflag(FLAG_GOT_MIRACLE_SEED_ROUTE_32)
            call(Route32_EventScript_RoadblockMan_AfterItem)
        }
    }
    else {
        msgbox("{PLAYER}, right?\nSome guy wearing glasses was\llooking for you.\pGo see for yourself. He's waiting\nfor you at the {POKEMON} CENTER.")
    }
}

script Route32_EventScript_RoadblockMan_AfterItem {
    msgbox("Your experiences in VIOLET CITY\nshould be useful for your journey.")
}

movement Route32_Movement_RoadblockMan_NoticePlayer {
    emote_exclamation_mark
    face_left
}

movement Route32_Movement_RoadblockMan_PlayerFromLeft {
    delay_16
    walk_up * 2
    face_down
}

movement Route32_Movement_RoadblockMan_ManFromLeft {
    walk_left * 2
    walk_up
}

movement Route32_Movement_RoadblockMan_PlayerFromRight {
    walk_up * 2
    face_down
}

movement Route32_Movement_RoadblockMan_ManFromRight {
    walk_left
    walk_up
}

movement Route32_Movement_RoadblockMan_ManResetFromLeft {
    walk_down
    walk_right * 2
    face_left
}

movement Route32_Movement_RoadblockMan_ManResetFromRight {
    walk_down
    walk_right
    face_left
}

script Route32_EventScript_SlowpokeTailManTrigger1 {
    lockall
    setvar(VAR_TEMP_1, 0)
    goto(Route32_EventScript_SlowpokeTailManTrigger_All)
}

script Route32_EventScript_SlowpokeTailManTrigger2 {
    lockall
    setvar(VAR_TEMP_1, 1)
    goto(Route32_EventScript_SlowpokeTailManTrigger_All)
}

script Route32_EventScript_SlowpokeTailManTrigger_All {
    textcolor(MSG_COLOR_BLUE)
    playse(SE_PIN)
    applymovement(SLOWPOKETAIL_MAN, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(SLOWPOKETAIL_MAN, Common_Movement_Delay48)
    waitmovement(0)
    msgbox("Hey, kid!\n"
    "Hold on a sec!")
    if (var (VAR_TEMP_1) == 0) {
        applymovement(OBJ_EVENT_ID_PLAYER, Route32_Movement_FaceUp)
        waitmovement(0)
    } else {
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, Route32_Movement_StepUp)
        waitmovement(0)
    }
    call(Route32_EventScript_SlowpokeTailMan_Common)
    releaseall
}

movement Route32_Movement_FaceUp {
    walk_in_place_fastest_up
}

movement Route32_Movement_StepUp {
    walk_up
}

script Route32_EventScript_SlowpokeTailMan {
    lock
    faceplayer
    call(Route32_EventScript_SlowpokeTailMan_Common)
    release
}

script Route32_EventScript_SlowpokeTailMan_Common {
    setvar(VAR_ROUTE32_STATE, 2)
    showmoneybox(0, 0, 0)
    msgbox("How would you like to have this\ntasty, nutritious SLOWPOKETAIL?\pFor you right now, it costs just\n¥1,000,000!\pYou'll want this!", MSGBOX_YESNO)
    hidemoneybox
    if (var (VAR_RESULT) == YES) {
        msgbox("Tch! And here I thought kids these\ndays were loaded…")
    } else {
        msgbox("You don't want it? You're sure?\nThen scram. Shoo!")
    }
}

script Route32_EventScript_RoarTMGuy {
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_TM05_ROAR)) {
        msgbox("WROOOOAR!\pPEOPLE RUN WHEN I ROAR!\nBUT YOU CAME LOOKING!\pTHAT PLEASES ME!\nNOW TAKE THIS!")
        giveitem(ITEM_TM05)
        if (!var (VAR_RESULT)) {
            goto(Common_EventScript_ShowBagIsFull)
        } else {
            setflag(FLAG_RECEIVED_TM05_ROAR)
        }
    }
    msgbox("WROOOAR!\nIT'S ROAR!\pEVEN {POKEMON} WILL RUN FROM A\nGOOD ROAR!")
    release
}

script Route32_EventScript_Frieda {
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_POISON_BARB_FROM_FRIEDA))
    {
        gettime
        if (var (VAR_0x8003) == DAY_FRIDAY)
        {
            if (!flag(FLAG_MET_FRIEDA_OF_FRIDAY))
            {
                msgbox("FRIEDA: Yahoo!\nIt's FRIDAY!\pI'm FRIEDA of FRIDAY!\nNice to meet you!\pHere's a POISON BARB for you!")
                setflag(FLAG_MET_FRIEDA_OF_FRIDAY)
            }
            else
            {
                msgbox("FRIEDA:Here's a POISON BARB\nfor you!")
            }
            giveitem(ITEM_POISON_BARB)
            if (!var(VAR_RESULT))
            {
                goto(Common_EventScript_ShowBagIsFull)
            }
            setflag(FLAG_RECEIVED_POISON_BARB_FROM_FRIEDA)
            msgbox("FRIEDA: Give it to a {POKEMON} that\nhas POISON-type moves.\pOh!\nIt's wicked!\pYou'll be shocked how good it makes\nPOISON-type moves!")
        }
        else
        {
            msgbox("FRIEDA: Isn't it FRIDAY today?\n"
            "It's so boring when it's not!")
        }
    }
    else
    {
        msgbox("FRIEDA: Hiya!\nWhat day do you like?\pI love FRIDAY.\nThere's no doubt about it!\pDon't you think it's great too?")
    }
    release
}

script Route32_EventScript_Albert {
    trainerbattle_single(TRAINER_ALBERT, "I haven't seen you around before.\nSo you think you're pretty tough?", "You're stronger than you look!")
    msgbox("I'm going to try to be the best\nwith my favorites.\pI'm not using the same tough\n{POKEMON} as everyone else.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Liz {
    trainerbattle_single(TRAINER_LIZ_1, "Uh-huh.\nYeah, and you know…\pPardon? You want to battle?\nI'm on the phone.\pOh, all right.\nBut let's make this fast.", "Oh!\nI've got to relieve my anger!")
    // TODO: Check phone strings for textbox fit

    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == 1)
    {
        trainerbattle_rematch(TRAINER_LIZ_1, "You're late!\nLet's get started now!", "Oh!\nI've got to relieve my anger!")
        clearflag(FLAG_LIZ_OFFERED_REMATCH)
    }

    if (!flag(FLAG_PHONE_CARD_PICNICKER_LIZ))
    {
        if (!flag(FLAG_PICNICKER_LIZ_OFFERED_NUMBER))
        {
            msgbox("Oh? You're on a big journey to see\nthe sights?\lNeat! Let's be friends!\lCan I have your phone number?\pI want to hear about everything\nyou've seen!", MSGBOX_YESNO)
            setflag(FLAG_PICNICKER_LIZ_OFFERED_NUMBER)
        }
        else
        {
            msgbox("Let's be friends! Can I have your\nphone number?\pI want to hear about everything\nyou've seen!", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == 1)
        {
            registerphonecontact(FLAG_PHONE_CARD_PICNICKER_LIZ) 
            msgbox("I'll telephone you if I hear\nanything interesting.", MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("Aww… You won't be my friend?", MSGBOX_AUTOCLOSE)
        }
        end
    }
    msgbox("I was having a nice chat, too.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Roland {
    trainerbattle_single(TRAINER_ROLAND, "That glance…\nIt's intriguing.", "Hmmm.\nThis is disappointing.")
    msgbox("If you don't want to battle, just\navoid eye contact.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Henry {
    trainerbattle_single(TRAINER_HENRY, "My {POKEMON}?\nThey're freshly caught!", "SPLASH?")
    msgbox("Freshly caught {POKEMON} are no\nmatch for properly raised ones.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Justin {
    trainerbattle_single(TRAINER_JUSTIN, "Whoa!\nYou made me lose that big fish!", "Sploosh!")
    msgbox("Calm, collected…\nThe essence of fishing and {POKEMON}\lis the same.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Ralph {
    trainerbattle_single(TRAINER_RALPH_1, "I'm really good at both fishing\nand {POKEMON}.\pI'm not about to lose to any kid!", "Tch!\nI tried to rush things…")

    // TODO: Check phone strings for textbox fit
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == 1)
    {
        trainerbattle_rematch(TRAINER_RALPH_1, "Hey kid!\nNo losing for me this day!", "Tch!\nI tried to rush things…")
        clearflag(FLAG_RALPH_OFFERED_REMATCH)
    }
    if (!flag(FLAG_PHONE_CARD_FISHER_RALPH))
    {
        if (!flag(FLAG_FISHER_RALPH_OFFERED_NUMBER))
        {
            msgbox("Don't young people fish anymore?\pI've seen rare {POKEMON} while I've\nbeen fishing…\lYou young people like that, right?\pWant to trade phone numbers?\nI'll let you know if I see anything.", MSGBOX_YESNO) 
            setflag(FLAG_FISHER_RALPH_OFFERED_NUMBER)
        }
        else
        {
            msgbox("Hm? So you do want me to phone if\nI see rare {POKEMON}?", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == 1)
        {
            registerphonecontact(FLAG_PHONE_CARD_FISHER_RALPH) 
            msgbox("Yep, phone if I see rare {POKEMON}.\nDon't worry, I won't forget!", MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("Oh… My own kids won't even give\nme their phone numbers…\lIs that how young people are today?", MSGBOX_AUTOCLOSE)
        }
        end
    }
    msgbox("Fishing is a lifelong passion.\n{POKEMON} are lifelong friends!", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Gordon {
    trainerbattle_single(TRAINER_GORDON, "I found some good {POKEMON} in the\ngrass around here!\pI think they'll do it for me!", "Darn.\nI thought I could win.")
    msgbox("The grass is full of clingy things.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_Peter {
    trainerbattle_single(TRAINER_PETER, "That BADGE!\nIt's from VIOLET CITY's GYM!\pYou beat FALKNER?", "I know what my weaknesses are.")
    msgbox("I should train again at the GYM\nin VIOLET CITY.", MSGBOX_DEFAULT)
    release
}

script Route32_EventScript_RuinsOfAlphSign {
    msgbox("RUINS OF ALPH\nEAST ENTRANCE", MSGBOX_SIGN)
}

script Route32_EventScript_DirectionsSign {
    msgbox("ROUTE 32\nVIOLET CITY - AZALEA TOWN", MSGBOX_SIGN)
}

script Route32_EventScript_UnionCaveSign {
    msgbox("UNION CAVE AHEAD", MSGBOX_SIGN)
}
