mapscripts EcruteakCity_PokemonCenter_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(HEAL_LOCATION_ECRUTEAK_CITY)
        if(flag(FLAG_MET_BILL_IN_ECRUTEAK))
        {
            setvar(VAR_TEMP_0, 1)
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: EcruteakCity_PokemonCenter_1F_Eventscript_Bill
    ]
    MAP_SCRIPT_ON_RESUME: CableClub_OnResume
}

script EcruteakCity_PokemonCenter_1F_Eventscript_Bill{
    lockall
    textcolor(0)
    applymovement(5, Movement_BillToCounter)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_PlayerToBill)
    waitmovement(0)
    delay(5)
    applymovement(1, Movement_PkmnCenterNurse_Bow)
    waitmovement(0)
    delay(10)
    applymovement(1, Movement_PkmnCenterNurse_Bow)
    waitmovement(0)
    delay(10)
    turnobject(5, DIR_SOUTH)
    msgbox("Hi, I'm BILL.\nAnd who are you?\pHmm, {PLAYER}, huh?\nYou've come at the right time.\pI just finished adjustments on the\nWIRELESS and CABLE CLUBS upstairs.\pYou know that {POKEMON} can be\ntraded, right?\pMy TIME CAPSULE modification was\ndeveloped to enable trades with\lthe past.\pDon't you worry.\nI'm done installing it.\pTomorrow, TIME CAPSULES will be\nrunning at all {POKEMON} CENTERS.\pI have to hurry on back to\nGOLDENROD and see my folks.\pBuh-bye!", MSGBOX_DEFAULT)
    closemessage
    applymovement(5, Movement_BillToExit1)
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    applymovement(5, Movement_BillToExit2)
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    applymovement(5, Movement_BillToExit3)
    waitmovement(0)
    removeobject(5)
    playse(SE_EXIT)
    waitse
    delay(10)
    setflag(FLAG_MET_BILL_IN_ECRUTEAK)
    clearflag(FLAG_HIDE_GOLDENROD_CITY_BILLS_FAMILYS_HOUSE_BILL)
    setvar(VAR_TEMP_0, 1)
    releaseall
    end
}

movement(local) Movement_BillToCounter{
    walk_right * 6
    walk_up * 2
    step_end
}

movement(local) Movement_PlayerToBill{
    walk_up * 3
    step_end
}

movement(local) Movement_BillToExit1{
    walk_right
    step_end
}

movement(local) Movement_BillToExit2{
    walk_down * 2
    step_end
}

movement(local) Movement_BillToExit3{
    walk_left
    walk_down * 2
    step_end
}

script EcruteakCity_PokemonCenter_1F_Eventscript_Girl{
    msgbox("MORTY, the GYM LEADER, is\nsoooo cool.\pHis {POKEMON} are really tough too.", MSGBOX_NPC)
    end
}

script EcruteakCity_PokemonCenter_1F_Eventscript_Man{
    msgbox("The LAKE OF RAGE…\pThe appearance of a GYARADOS\nswarm…\pI smell a conspiracy.\nI know it!", MSGBOX_NPC)
    end
}

script EcruteakCity_PokemonCenter_1F_Eventscript_Hiker{
    msgbox("The way the KIMONO GIRLS dance is\nmarvelous. Just like the way they\luse their {POKEMON}.", MSGBOX_NPC)
    end
}