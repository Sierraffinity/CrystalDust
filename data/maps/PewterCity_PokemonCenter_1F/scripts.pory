mapscripts PewterCity_PokemonCenter_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(HEAL_LOCATION_PEWTER_CITY)
    }
    MAP_SCRIPT_ON_RESUME: CableClub_OnResume
}

script PewterCity_PokemonCenter_1F_EventScript_Woman{
    msgbox("…Yeah, and the GYM in CINNABAR's\ngone. I was really amazed.\p…Yes? I'm on the phone.\nGo away!", MSGBOX_NPC)
    end
}


script PewterCity_PokemonCenter_1F_EventScript_Youngster{
    msgbox("Most {POKEMON} get drowsy if they\nhear a JIGGLYPUFF singing.\pThere are several moves that can be\nused only while a {POKEMON} is asleep.", MSGBOX_NPC)
    end
}

script PewterCity_PokemonCenter_1F_EventScript_Jigglypuff {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_JIGGLYPUFF, 0)
    msgbox("JIGGLYPUFF: Puu pupuu.")
    waitmoncry
    release
}

script PewterCity_PokemonCenter_1F_EventScript_Chris {
    lock
    faceplayer
    setvar(VAR_0x8008, INGAME_TRADE_XATU)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)

    if (!flag(FLAG_PEWTER_CITY_NPC_TRADE_COMPLETED))
    {
        msgbox("Hi, I'm looking for this {POKEMON}.\pIf you have {STR_VAR_1}, would\nyou trade it for my {STR_VAR_2}?", MSGBOX_YESNO)
        
        if (var(VAR_RESULT) != NO)
        {
            special(ChoosePartyMon)
            waitstate
            lock
            faceplayer
            copyvar(VAR_0x800A, VAR_0x8004)
            
            if (var(VAR_0x8004) != 0xFF)
            {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies) // dumps selected party species to VAR_RESULT
                copyvar(VAR_0x800B, VAR_RESULT)

                if (var(VAR_RESULT) == VAR_0x8009) // 0x8009 is requested species
                {
                    msgbox("OK, connect the Game Link Cable.", MSGBOX_DEFAULT)
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)
                    waitstate
                    lock
                    faceplayer
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Great! Thank you!\nI finally got {STR_VAR_1}.")
                    setflag(FLAG_PEWTER_CITY_NPC_TRADE_COMPLETED)
                }
                else
                {
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("You don't have {STR_VAR_1}?\nThat's too bad, then.")
                }
            }
            else
            {
                msgbox(OlivineCity_House2_Text_Tim_NoTrade)
            }
        }
        else
        {
            msgbox(OlivineCity_House2_Text_Tim_NoTrade)
        }
    }
    else
    {
        bufferspeciesname(0, VAR_0x8009)
        msgbox("Hi! The {STR_VAR_1} you traded me is\ndoing great!")
    }

    release
}