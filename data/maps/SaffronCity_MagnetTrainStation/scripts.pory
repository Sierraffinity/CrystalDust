mapscripts SaffronCity_MagnetTrainStation_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        setvar(VAR_TEMP_0, 1)
        getplayerxy(VAR_RESULT, VAR_0x8008)
        if(var(VAR_RESULT) == 15 && var(VAR_0x8008) == 5)
        {
            setobjectxyperm(4, 14, 7)
            setobjectmovementtype(4, MOVEMENT_TYPE_FACE_LEFT)
            setvar(VAR_TEMP_0, 0)
        }
        if(!flag(FLAG_RETURNED_MACHINE_PART))
        {
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_0, 0 {
            applymovement(OBJ_EVENT_ID_PLAYER, SaffronCity_MagnetTrainStation_Movement_PlayerExitsTrain)
            waitmovement(0)
            applymovement(4, SaffronCity_MagnetTrainStation_Movement_OfficerRepositions)
            waitmovement(0)
            setobjectxyperm(4, 13, 9)
            setobjectmovementtype(4, MOVEMENT_TYPE_FACE_DOWN)
            setvar(VAR_TEMP_0, 1)
            textcolor(0)
            msgbox("We have arrived in SAFFRON.\nWe hope to see you again.", MSGBOX_DEFAULT)
            releaseall
            end
        }
    ]
}movement SaffronCity_MagnetTrainStation_Movement_PlayerExitsTrain{
    walk_down
    walk_left * 2
    walk_down * 4
    face_up
}movement SaffronCity_MagnetTrainStation_Movement_OfficerRepositions{
    walk_left
    walk_down * 2
}

script SaffronCity_MagnetTrainStation_EventScript_Officer{
    lock
    faceplayer
    if(flag(FLAG_RETURNED_MACHINE_PART))
    {
        msgbox("We'll soon depart for GOLDENROD.\nAre you coming on board?", MSGBOX_YESNO)
        if(!var(VAR_RESULT))
        {
            msgbox("We hope to see you again.", MSGBOX_DEFAULT)
            release
            end
        }
        checkitem(ITEM_PASS, 1)
        if(!var(VAR_RESULT))
        {
            msgbox("Sorry, but you don't have a PASS.", MSGBOX_DEFAULT)
            release
            end
        }
        msgbox("May I see your rail PASS, please?\nOK. Right this way, please.", MSGBOX_DEFAULT)
        closemessage
        applymovement(4, SaffronCity_MagnetTrainStation_Movement_OfficerMovesOut)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, SaffronCity_MagnetTrainStation_Movement_PlayerApproachTrain)
        waitmovement(0)
        //play Magnet Train scene
        warp(MAP_GOLDENROD_CITY_MAGNET_TRAIN_STATION, 0xFF, 15, 5)
        waitstate
        end
    }
    msgbox("I'm sorry, but the MAGNET TRAIN\nisn't operating now.", MSGBOX_NPC)
    release
    end
}movement(local) SaffronCity_MagnetTrainStation_Movement_OfficerMovesOut{
    walk_up * 2
    walk_right
    face_left
}movement SaffronCity_MagnetTrainStation_Movement_PlayerApproachTrain{
    walk_up * 3
    walk_left * 3
    walk_up * 2
}

script SaffronCity_MagnetTrainStation_EventScript_GymGuide{
    if(flag(FLAG_RETURNED_MACHINE_PART))
    {
        msgbox("Whew…\pHow many times have I gone back\nand forth between KANTO and JOHTO?", MSGBOX_NPC)
        end
    }
    msgbox("The MAGNET TRAIN is a super-modern\nrail liner that uses electricity\land magnets to attain incredible\lspeed.\pHowever, if there isn't any\nelectricity…", MSGBOX_NPC)
    end
}

script SaffronCity_MagnetTrainStation_EventScript_Teacher{
    msgbox("Before the MAGNET TRAIN STATION was\nbuilt, there was a house there.\pA little girl named COPYCAT used\nto live there.", MSGBOX_NPC)
    end
}

script SaffronCity_MagnetTrainStation_EventScript_Lass{
    msgbox("Hi. Do you have a rail PASS?\nI have one.\pAll the people in SAFFRON who\nride the MAGNET TRAIN have passes.", MSGBOX_NPC)
    end
}