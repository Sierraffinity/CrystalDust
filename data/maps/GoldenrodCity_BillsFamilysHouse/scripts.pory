mapscripts GoldenrodCity_BillsFamilysHouse_MapScripts {}

script GoldenrodCity_BillsFamilysHouse_EventScript_Bill {
    lock
    faceplayer
    if (flag(FLAG_GOT_EEVEE_FROM_BILL))
    {
        msgbox("BILL: My pop, he won't work. All he\n"
               "does is goof off all day long.\p"
               "He's getting to be a real headache…")
    }
    else
    {
        msgbox("BILL: Hi, {PLAYER}!\n"
               "Do us a favor and take this EEVEE.\p"
               "It came over when I was adjusting\n"
               "the TIME CAPSULE.\p"
               "Someone has to take care of it,\n"
               "but I don't like being outside.\p"
               "Can I count on you to play with\n"
               "it, {PLAYER}?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES)
        {
            msgbox("BILL: I knew you'd come through!\n"
                   "Way to go! You're the real deal!\p"
                   "Okay, I'm counting on you.\n"
                   "Take good care of it!")
            textcolor(MSG_COLOR_SYS)
            specialvar(VAR_RESULT, CalculatePlayerPartyCount)
            if(var(VAR_RESULT) == 6)
            {
                goto(GoldenrodCity_BillsFamilysHouse_EventScript_Bill_PartyFull)
            }
            givemon(SPECIES_EEVEE, 20, ITEM_NONE)
            if (var(VAR_RESULT) == 0)
            {
                call(GoldenrodCity_BillsFamilysHouse_EventScript_ReceivedEeveeFanfare)
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES)
                {
                    call(Common_EventScript_GetGiftMonPartySlot)
                    call(Common_EventScript_NameReceivedPartyMon)
                }
            }
            elif (var(VAR_RESULT) == 1)
            {
                call(GoldenrodCity_BillsFamilysHouse_EventScript_ReceivedEeveeFanfare)
                msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
                if (var(VAR_RESULT) == YES)
                {
                    call(Common_EventScript_NameReceivedBoxMon)
                }
                call(Common_EventScript_TransferredToPC)
            }
            else
            {
                goto(GoldenrodCity_BillsFamilysHouse_EventScript_Bill_PartyFull)
            }
            textcolor(MSG_COLOR_PREV)
            setflag(FLAG_GOT_EEVEE_FROM_BILL)
            msgbox("BILL: PROF. ELM claims EEVEE may\n"
                   "evolve in new and unknown ways.")
        }
        else
        {
            msgbox("Oh…\n"
                   "Now what to do?")
        }
    }
    release
}

script GoldenrodCity_BillsFamilysHouse_EventScript_Bill_PartyFull {
    textcolor(MSG_COLOR_PREV)
    msgbox("Whoa, wait.\n"
           "You can't carry any more {POKEMON}.")
    release
}

script GoldenrodCity_BillsFamilysHouse_EventScript_ReceivedEeveeFanfare {
    bufferspeciesname(0, SPECIES_EEVEE)
	playfanfare(MUS_OBTAIN_ITEM)
	message("{PLAYER} received the {STR_VAR_1}\n"
            "from BILL.")
	waitmessage
	waitfanfare
}

script GoldenrodCity_BillsFamilysHouse_EventScript_BillsMom {
    lock
    faceplayer
    if (flag(FLAG_MET_BILL_IN_ECRUTEAK))
    {
        msgbox("My husband was once known as a\n"
               "{POKE}MANIAC. BILL must have taken\l"
               "after his father.")
    }
    else
    {
        msgbox("Oh, you collect {POKEMON}?\n"
               "My son BILL is an expert collector.\p"
               "He just got called to the {POKEMON}\n"
               "CENTER in ECRUTEAK CITY.\p"
               "My husband went off to the GAME\n"
               "CORNER without being called…")
    }
    release
}

script GoldenrodCity_BillsFamilysHouse_EventScript_BillsSister {
    lock
    faceplayer
    if (!flag(FLAG_PHONE_CARD_BILL))
    {
        msgbox("Are you a TRAINER? I've got a\n"
               "useful phone number for you.\p"
               "Do you want it?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES)
        {
            waitse
            setflag(FLAG_PHONE_CARD_BILL)
            playfanfare(MUS_REGISTER_PHONE)
            textcolor(MSG_COLOR_SYS)
            msgbox("{PLAYER} recorded BILL's number.")
            waitfanfare
            textcolor(MSG_COLOR_PREV)
            msgbox(GoldenrodCity_BillsFamilysHouse_Text_BillsSister_AfterPhoneNumber)
        }
        else
        {
            msgbox("My brother made the PC {POKEMON}\n"
                   "storage system.\p"
                   "I was going to give you BILL's\n"
                   "number…")
        }
    }
    else
    {
        msgbox(GoldenrodCity_BillsFamilysHouse_Text_BillsSister_AfterPhoneNumber)
    }
    release
}

text GoldenrodCity_BillsFamilysHouse_Text_BillsSister_AfterPhoneNumber {
    "My big brother BILL made the PC\n"
    "{POKEMON} storage system."
}
